@c gstranpt.texi
 
@c This file is part of the 3DLDF User and Reference Manual.
@c Copyright (C) 2003, 2004, 2005, 2006, 2007, 2008, 2009, 2010, 2011, 2012, 2013, 2014,
@c 2015, 2016, 2017, 2018, 2019, 2020, 2021 The Free Software Foundation, Inc. 
@c See the section "GNU Free Documentation License" in the file 
@c fdl-1.3.texi for copying conditions.

@c * (1) Top

@node Transforming Points, Transforms, Points, Top
@chapter Transforming Points

@code{points} don't always have to remain in the same place.  There are
various ways of moving or @dfn{transforming}@c
@c
@footnote{The transformations described in this chapter are known as
@dfn{affine transformations}.  They have the property that parallelity
of lines is maintained.  That is, if two lines (each determined by two
points) are parallel before the transformation, they will also be
parallel after the transformation.  Affine transformations are
discussed in many books about computer graphics and geometry.  For
3DLDF, I've mostly used Jones, @cite{Computer Graphics through Key Mathematics}
and Salomon, @cite{Computer Graphics and Geometric Modeling}.}
@c
them:
@itemize @bullet
@item
Shifting.  This is often called ``translating'', but the operation in @MF{}
that performs translation is called @code{shift}, so I call it ``shifting''.

@item
Scaling.

@item
Shearing.

@item
Rotating about an axis.

@end itemize

@menu
* Shifting Points::             
* Scaling Points::              
* Shearing Points::             
* Rotating Points::             
@end menu

@c ** (2)

@node Shifting Points, Scaling Points, Transforming Points, Transforming Points
@section Shifting

In 3DLDF, @code{points} can be shifted entirely analogously to the way
@code{pairs} are shifted in @MF, albeit with a third argument for the 
keyword @code{shifted}.  The arguments are added to the x-, y- and
z-coordinates, respectively, of the @code{point}.  The arguments for
the y- and z-coordinates are optional.  If only a single argument is
supplied, i.e., for the x-coordinate, the parentheses are optional:

@example
point p, q, r, s;
p := origin shifted (1, 2, 3); %% origin == (0, 0, 0)
show p;
@print{}
(1.00000000, 2.00000000, 3.00000000, 1.00000000)
q := origin shifted (5, 6);
show q;
@print{}
(5.00000000, 6.00000000, 0.00000000, 1.00000000)
r := origin shifted (7);
show r;
@print{}
(7.00000000, 0.00000000, 0.00000000, 1.00000000)
s := origin shifted 8;
show s;
@print{}
(8.00000000, 0.00000000, 0.00000000, 1.00000000)
@end example

@code{points} need not be shifted using numerical (@code{real})
arguments:  The argument to @code{shifted} may be another
@code{point}.   In this case, the x-, y- and z-coordinates of the
@code{point} passed as an argument are added to the corresponding
coordinates of the @code{point} being shifted:

@example
point p, q, r, s;
p := (1, 2, 3);
q := (10, 20, 30);
r := p shifted by q;
show r;
@print{}
(11.00000000, 22.00000000, 33.00000000, 1.00000000)
s := r shifted q;
@print{}
(21.00000000, 42.00000000, 63.00000000, 1.00000000)
@end example

In this case, the keyword @code{by} is ``syntactic sugar'', i.e., it's
use is optional.  However, if the argument to @code{shifted} is a
@dfn{point expression}, i.e., an @dfn{expression} whose value is a
@code{point}, rather than a @code{point} itself, the @code{keyword}
@code{by} @emph{must} be used:

@example
point p, q, r, s;
p := (1, 2, 3);
q := (10, 20, 30);
r := origin shifted by (p + q);  %% Correct
show r;
@print{}
(11.00000000, 22.00000000, 33.00000000, 1.00000000)
r := origin shifted (p + q);  %% Incorrect
@error{}
ERROR! In yyparse(): syntax error
[Thread 1] ERROR! In `main()':
`yyparse()' returned 1.
@end example

The @MF{} language was designed to be @dfn{declarative} (as opposed to
@dfn{imperative}).  Statements in a program should be @emph{assertions} rather
than commands telling the computer what to do.  This was an idea that was in the
air at the time Donald Knuth was developing @MF{}, however, with all due
respect, the author feels that it has not stood the test of time.

Therefore, 3DLDF implements @emph{commands} corresponding to the keywords
@code{shifted}, @code{scaled}, @code{rotate}, etc., which can be used for
transforming @code{points} and other objects---unless you're afraid of hurting
the computer's feelings.  The commands are named @code{shift}, @code{scale},
@code{rotate}, etc.:

%% !! TODO: LDF 2021.09.14.  Explain why "by" is needed.

@example
point p, q, r;
p := q := r := origin;
shift p by 1; %% "by" is required
show p;
@print{}
(1.00000000, 0.00000000, 0.00000000, 1.00000000)
shift q (2, 3); 
show q;
@print{}
(2.00000000, 3.00000000, 0.00000000, 1.00000000)
shift r (4, 5, 6);
show r;
@print{}
(4.00000000, 5.00000000, 6.00000000, 1.00000000)
shift r by p; %% point argument, "by" is required.
show r;
@print{}
(5.00000000, 5.00000000, 6.00000000, 1.00000000)
@end example

Yet another way of shifting @code{points} is to use the @code{+=} or
@code{-=} operator with a @code{point} or @code{point expression} argument:

@example
point a, b, c;
a := (1, 1, 1);
b := (2, 2, 2);
a += b;
show a;
@print{}
(3.00000000, 3.00000000, 3.00000000, 1.00000000)
b -= a;
show b;
(-1.00000000, -1.00000000, -1.00000000, 1.00000000)
@end example

The @code{+=} and @code{-=} operators (as well as the @code{*=} and @code{/=} operators)
only exist in 3DLDF, i.e., not in @MF{} or @MP{}.  In fact, they are exceptions
to the normal scanning rules inherited from @MF{}.
@xref{How 3DLDF Reads What You Type}.

@c ** (2)

@node Scaling Points, Shearing Points, Shifting Points, Transforming Points
@section Scaling

The keyword @code{scaled} takes one to three numerical (@code{real}) arguments.  
If three arguments are supplied, the x-, y-, and z-coordinates of the @code{point} are 
multiplied by the first, second, and third arguments respectively.  

If only one argument is supplied, the x-, y- and z-coordinates of the
@code{point} are all multiplied by the argument.  The argument may optionally be
surrounded by parentheses.  Please note that this implies that to scale only the
x-coordinate, a dummy argument of 1 must be supplied for the y-coordinate,
otherwise, all three coordinates will be multiplied by the argument.

If two arguments are supplied, the x-coordinate of the @code{point} is
multiplied by the first argument and the y-coordinate is multiplied by the
second.  The z-coordinate remains unchanged.  

If one wants to perform scaling in either the y-dimension only, a
dummy argument of 1 must be passed for scaling in the x-dimension.
Similarly, if one wants to perform scaling in the z-dimension only,
dummy arguments of 1 must be passed for scaling in the x and y-dimensions.

@example
point p[];
p0 := (1, 1, 1);
p1 := p0 scaled (2, 3, 4);
show p1;
@print{}
(2.00000000, 3.00000000, 4.00000000, 1.00000000)

p2 := p0 scaled 2;
show p2;
@print{}
(2.00000000, 2.00000000, 2.00000000, 1.00000000)
  
scale p0 (1, 3);
show p0;
@print{}
(1.00000000, 3.00000000, 1.00000000, 1.00000000)
  
scale p0 (1, 1, 3);
show p0;
%%@print{}
(1.00000000, 3.00000000, 3.00000000, 1.00000000)
@end example

For scaling a single coordinate of a @code{point}, 3DLDF implements the keywords
@code{xscaled}, @code{yscaled} and @code{zscaled} and the corresponding commands
@code{xscale}, @code{yscale} and @code{zscale}:

@example
point p, q;
p := (1, 1, 1);
q := p xscaled 2;
show q;
@print{} 
(2.00000000, 1.00000000, 1.00000000, 1.00000000)
q := p yscaled 3;
show q;
@print{}
(1.00000000, 3.00000000, 1.00000000, 1.00000000)
q := p zscaled 4;
show q;
@print{}
(1.00000000, 1.00000000, 4.00000000, 1.00000000)
xscale p by 5;
show p;
@print{}
(5.00000000, 1.00000000, 1.00000000, 1.00000000)
yscale p by 6;
show p;
@print{}
(5.00000000, 6.00000000, 1.00000000, 1.00000000)
zscale p by 7;
show p;
@print{}
(5.00000000, 6.00000000, 7.00000000, 1.00000000)
@end example

@c ** (2)

@node Shearing Points, Rotating Points, Scaling Points, Transforming Points
@section Shearing

Shearing is more complicated than shifting or scaling.  The @code{sheared}
keyword takes six numerical arguments.
If @math{p} and @math{q} are @code{points}, then @code{q := p sheared (a, b, c, d, e, f)} sets
@math{x_q} to @math{x_q + ay_q + bz_q}, @math{y_q} to 
@math{y_q + cx_q + dz_q}, and  
@math{z_q} to @math{z_q + ex_q + fy_q}.
In this way, each (non-w) coordinate of a @code{point} is modified based on the
values of the other two coordinates, whereby the influence of the
other coordinates on the new value is weighted according to the
arguments. 

@example
point p, q;
p := (1, 1, 1);
q := p sheared (1);
show q;
@print{}
(2.00000000, 1.00000000, 1.00000000, 1.00000000)
q := p sheared (1, 1);
show q;
@print{} 
(3.00000000, 1.00000000, 1.00000000, 1.00000000)
q := p sheared (1, 1, 2, 2, 3, 3);
show q;
@print{}
(3.00000000, 5.00000000, 7.00000000, 1.00000000)
@end example

@c !! START HERE!  Sat 22 May 2021 10:50:21 AM CEST
@c Rewrite this example.  

@c I've copied the following example from 
@c the node ``Affine Transformations for Points''.
@c
@NEXTFIG{} demonstrates the effect of shearing the points of a rectangular
@code{path} in the x-y plane.

@example
path p;
p := (0, 0) -- (4, 0) -- (4, 3) -- (0, 3) -- cycle;
draw p;
shear p (1.5);
draw p dashed evenly;
@end example

@iftex
@tex
\vskip2\baselineskip
\leftline{\epsffile{examples.1}}
@end tex
@end iftex

@c ** (2) Rotating Points

@node Rotating Points,  , Shearing Points, Transforming Points
@section Rotating

@c !! TO DO:  Make illustration. 

The function @code{rotate} rotates a @code{Point} about one or more of
the main axes.
It takes three @code{real} arguments, specifying the 
angles of rotation in degrees about the x, y, and z-axes respectively.
Only the first argument is required, the other two are 0 by default.  If
rotation about the y-axis, or the y and z-axes only are required, then 0
must be used as a placeholder for the first and possibly the second
argument.  

@example
Point p(0, 1);
p.rotate(90);
p.show("p:");
@print{} p: (0, 0, -1)
p.rotate(0, 90);
p.show("p:");
@print{} p: (1, 0, 0)
p.rotate(0, 0, 90);
p.show("p:");
@print{} p: (0, 1, 0)
@end example

The rotations are performed successively about the 
x, y, and z-axes.  However, rotation is not a commutative
operation, so if rotation about the main axes in a different
order is required, then @code{rotate} must be invoked more than once:

@example
Point A(2, 3, 4);
Point B(A);
A.rotate(30, 60, 90);
A.show("A:");
@print{} A: (-4.59808, -0.700962, 2.7141)
B.rotate(0, 0, 90);
B.rotate(0, 60);
B.rotate(30);
B.show("B:");
@print{} B: (-4.9641, 1.43301, -1.51795)
@end example
 
Rotation need not be about the main axes;  it can also be performed
about a line defined by two @code{Points}.  The function @code{rotate}
with two @code{Point} arguments and a @code{real} argument for the
angle of rotation (in degrees) about the axis.  The @code{real} argument
is optional, with 
@DEG{180}
as the default.

@c !! TO DO:  Simplify this example.  How did I get the original values
@c for @code{p0} and @code{p1}? 
@c Make illustration larger, put values in illustration.

@example
Point p0 (-1.06066, 0, 1.06066);
Point p1 (1.06066, 0, -1.06066);
p1 *= p0.rotate(0, 30, 30);
p0.show("p0:");
@print{} p0: (-1.25477, -0.724444, 0.388228)
p1.show("p1:");
@print{} p1: (1.25477, 0.724444, -0.388228)
p0.draw(p1);
Point p2(1.06066, 0, 1.06066);
p2.show("p2:");
@print{} p2: (1.06066, 0, 1.06066)
Point p3(p2);
p3.rotate(p1, p0, 45);
p3.show("p3:");
@print{} p3 (1.09721, 1.15036, 1.17879)
Point p4(p2);
p4.rotate(p1, p0, 90);
p4.show("p4:"); 
@print{} p4: (0.882625, 2.05122, 0.485242)
Point p5(p2);
p5.rotate(p1, p0, 135);
p5.show("p5:");
@print{} p5: (0.542606, 2.17488, -0.613716)
Point p6(p2);
p6.rotate(p1, p0);
p6.show("p6:");
@print{} p6: (0.276332, 1.44889, -1.47433)
@end example

@c @iftex
@c @tex  
@c \ifmakeexamples
@c \BEX
@c \BGRP
@c \immediate\write\examples{default_focus.set(4, 5, -10, 4, 5, 10, 10);}
@c \immediate\write\examples{draw_axes(gray);}
@c \immediate\write\examples{Point p0 (-1.06066, 0, 1.06066);}
@c \immediate\write\examples{Point p1 (1.06066, 0, -1.06066);}
@c \immediate\write\examples{p1 *= p0.rotate(0, 30, 30);}
@c \immediate\write\examples{p0.dotlabel("p0", "lft"); }
@c \immediate\write\examples{p1.dotlabel("p1"); }
@c \immediate\write\examples{p0.draw(p1);}
@c \immediate\write\examples{Point p2(1.06066, 0, 1.06066);}
@c \immediate\write\examples{p2.dotlabel("p2", "rt"); }
@c \immediate\write\examples{Point p3(p2);}
@c \immediate\write\examples{p3.rotate(p1, p0, 45);}
@c \immediate\write\examples{p3.dotlabel("p3", "rt"); }
@c \immediate\write\examples{Point p4(p2);}
@c \immediate\write\examples{p4.rotate(p1, p0, 90);}
@c \immediate\write\examples{p4.dotlabel("p4"); }
@c \immediate\write\examples{Point p5(p2);}
@c \immediate\write\examples{p5.rotate(p1, p0, 135);}
@c \immediate\write\examples{p5.dotlabel("p5"); }
@c \immediate\write\examples{Point p6(p2);}
@c \immediate\write\examples{p6.rotate(p1, p0);}
@c \immediate\write\examples{p6.dotlabel("p6", "lft"); }
@c \immediate\write\examples{Path pa("..", false, &p2, &p3, &p4, &p5, &p6, 0);}
@c \immediate\write\examples{pa.draw();}
@c \OEX{}
@c \EGRP
@c \EEX 1
@c \fi
@c \PEX
@c @end tex
@c @end iftex

@ifhtml
@html
<p align="center">
<br>
<img src="./graphics/png/3DLDF2.png"
alt="[Figure 2. Not displayed.]"
/>
<br>
<br>
Fig. 2.
<br>
</p>
@end html
@end ifhtml

@ifinfo
[Figure 2 not displayed.]
@end ifinfo

@cindex bugs 
I have sometimes gotten erroneous results using @code{rotate} for
rotation about two @code{Points}.  It's usually worked to reverse the
order of the @code{Point} arguments, or to change sign of the angle
argument.  I think I've fixed the problem, though.

@c ** (2)

@c * (1) Local Variables for Emacs

@c For use in indirect buffers:
@c
@c (progn (texinfo-mode) (outline-minor-mode t) (setq outline-regexp "@c *\\*+") (ignore (quote 
@c )))
  
@c Local Variables:
@c mode:Texinfo
@c abbrev-mode:t
@c eval:(outline-minor-mode t)
@c outline-regexp:"@c *\\*+"
@c fill-column:80
@c End:
